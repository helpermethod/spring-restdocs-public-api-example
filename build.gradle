
apply plugin: 'groovy'
apply plugin: 'org.asciidoctor.convert'
apply plugin: 'codenarc'

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.3'
    }
}

dependencies {
    compile 'commons-logging:commons-logging:1.1.1'
    testCompile 'org.apache.logging.log4j:log4j-core:2.2'
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    testCompile "org.springframework.restdocs:spring-restdocs-restassured:${springRestDocsVersion}"
}

ext {
    snippetsDir = file('src/docs/generated-snippets')
}

asciidoctor {
    dependsOn test
    sourceDir = file('src/docs')
    inputs.dir snippetsDir
    backends 'html5'
    attributes 'source-highlighter' : 'prettify',
            'imagesdir':'images',
            'toc':'left',
            'icons': 'font',
            'setanchors':'true',
            'idprefix':'',
            'idseparator':'-',
            'docinfo1':'true',
            'snippets': snippetsDir
}

task cleanTempDir(type: Delete) {
    delete fileTree(dir: 'src/main/resources/public')
    delete fileTree(dir: 'src/docs/generated-snippets')
}

clean {
    dependsOn cleanTempDir
}

build {
    dependsOn asciidoctor
}

codenarcMain {
    configFile file('gradle/codenarc/codenarc.groovy')
}

codenarcTest {
    configFile file('gradle/codenarc/codenarcTest.groovy')
}

task publish(type: GradleBuild) {
    dependsOn asciidoctor
    buildFile = file('publish.gradle')
    tasks = ['publishGhPages']
}
